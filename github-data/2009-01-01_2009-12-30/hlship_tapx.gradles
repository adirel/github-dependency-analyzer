description = "Collection of add-ons for Tapestry 5"

ext.tapestryVersion = "5.3.6"

if (!project.hasProperty('deployUserName')) ext.deployUserName = '*UNSET-USERNAME*'
if (!project.hasProperty('deployPassword')) ext.deployPassword = '*UNSET-PASSWORD*'

apply plugin: 'idea'

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy' // mostly for testing
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    
    sourceCompatibility = '1.5'
    targetCompatibility = '1.5'
    
    version = '1.2'

    group = 'com.howardlewisship'
    
    repositories {
        mavenCentral()
               
        // All things JBoss/Javassist/Hibernate
        mavenRepo name: "JBoss", url: "https://repository.jboss.org/nexus/content/repositories/releases/"
        
        // For the Kaptcha library
        mavenRepo name: "HLS", url: "http://howardlewisship.com/repository/"

    	// Latest preview packages

      mavenRepo name: "Apache Staging", url: "https://repository.apache.org/content/groups/staging"
    }
    
    configurations {
        deployerJars
        provided
    }

    // See http://jira.codehaus.org/browse/GRADLE-784
    
    sourceSets {
      main { 
        compileClasspath += configurations.provided 
      }
    }
    
    dependencies {
        compile "org.apache.tapestry:tapestry-core:$tapestryVersion"        
        testCompile "org.apache.tapestry:tapestry-test:$tapestryVersion"        
        testCompile "org.easymock:easymock:3.0"
        groovy "org.codehaus.groovy:groovy-all:1.7.10"
        
        deployerJars "org.apache.maven.wagon:wagon-ftp:1.0-beta-2"        
    }
    
    test {
        useTestNG()
        
        options.suites("src/test/conf/testng.xml")
    }
    
    task sourcesJar(type: Jar, dependsOn:classes) {
       classifier = 'sources'
       from sourceSets.main.allSource
    }

    artifacts {
       archives sourcesJar
    }    

    
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            // deployUserName and deployPassword should be specified in ~/.gradle/gradle.properties

            snapshotRepository(url: "ftp://howardlewisship.com/snapshot-repository") {
              authentication(userName: deployUserName, password: deployPassword)
            }
            
            repository(url: "ftp://howardlewisship.com/repository") {
              authentication(userName: deployUserName, password: deployPassword)
            }
        }
    }
    
    idea.module {
      scopes.PROVIDED.plus += configurations.provided
      javaVersion = "inherited"
    }
}

idea.project {
  languageLevel = "1.6"
}
description = "Common general components"

dependencies {
  provided "javax.servlet:servlet-api:2.4"      
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.core.services.CoreModule'
    }
}description = "Improved JavaScript date/time selection control"

dependencies {
    compile project(':tapx-core')
    provided "javax.servlet:servlet-api:2.5"
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.datefield.services.DateFieldModule'
    }
}description = "Common general components"

ext.versions = [
        jetty : "8.0.3.v20111011",
        mongo : "2.6.5"
]

dependencies {
    compile "org.eclipse.jetty:jetty-webapp:$versions.jetty"
    compile "org.eclipse.jetty:jetty-nosql:$versions.jetty"
    compile "org.mongodb:mongo-java-driver:$versions.mongo"
    compile "commons-cli:commons-cli:1.2"
}

description = "Basic support for JMS services."

dependencies {
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.jms.core.CoreJMSModule'
    }
}description = "Utilities for converting arbitrary objects to native JSON values."

dependencies {
    compile "org.apache.tapestry:tapestry-ioc:$tapestryVersion"
    testCompile "org.spockframework:spock-core:0.5-groovy-1.7"
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.json.JSONModule'
    }
}

test {
    useJUnit()
}description = "Adds plainmessage: binding prefix"

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.plainmessage.services.PlainMessageModule'
    }
}description = "Transforms Tapestry into an off-line e-mail generator"

dependencies {
    compile "javax.mail:mail:1.4"
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.templating.services.TemplateModule'
    }
}description = "Packages YUI 2.7 and provides easy declarative access to the scripts"

dependencies {
    compile project(":tapx-core")
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'com.howardlewisship.tapx.yui.services.YuiModule'
    }
}