apply plugin: 'android'
apply plugin: 'apt'
apply plugin: 'hockeyApp'
apply plugin: 'android-test'



android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode buildVersionCode()
        versionName buildVersionName()
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        applicationVariants.all { variant ->
            def file = variant.outputFile

            //noinspection GroovyAssignabilityCheck
            variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + buildVersionName() + "-b" + buildVersionCode() + ".apk"))
        }
    }
    signingConfigs {
        release
    }
}


dependencies {
    apt "com.squareup.dagger:dagger-compiler:1.2.1"
    apt "com.jakewharton:butterknife:4.0.1"
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.flurry.android:FlurryAnalytics:3.3.3'
    compile 'com.squareup.okhttp:okhttp:1.3.0'
    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile 'com.willowtreeapps:oak-library:1.3.6'
    compile 'com.squareup.retrofit:retrofit:1.4.1'
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    /**
     * Unit Test dependencies
     * Required to run Unit Tests
     */
    androidTestCompile 'junit:junit:4.+'
    androidTestCompile 'org.robolectric:robolectric:2.1.+'
}

/**
 *
 * Start WTA specific config for Building on Teamcity
 *
 */

/**
 * Set up your keystore in the app directory (not the parent, at least not for now)
 * You can add the keystore to your gradle.properties, just don't put the passwords there, obvs.
 * keystorePassword, alias, and aliasPassword are all fed in through command line arguments on the build server
 */
if (project.hasProperty('keyStore') &&
        project.hasProperty('keyStorePassword') &&
        project.hasProperty('keyStoreAlias') &&
        project.hasProperty('keyStoreAliasPassword')) {
    android.signingConfigs.release.storeFile = file(project.properties.keyStore)
    android.signingConfigs.release.storePassword = project.properties.keyStorePassword
    android.signingConfigs.release.keyAlias = project.properties.keyStoreAlias
    android.signingConfigs.release.keyPassword = project.properties.keyStoreAliasPassword
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

hockeyapp {
    apiToken = "YOUR_HOCKEY_API_TOKEN" // Your API token.
    releaseType = 2 // 0 = Beta; 1 = Live; 2 = Alpha
    notify = 0 // 0 = No Notify; 1 = Notify all testers that can install
    status = 2 // 1 = No users can download; 2 = available to download for users
    outputDirectory = file("build/apk/")
    // Directory where builds are output. Should always be build/apk
    appFileNameRegex = ".*(release).*\\.apk\$"
    // Regex for file. This regex does the release version
    notesType = 1 // 0 = Textile Format; 1 = Markdown Format
    notes = "Some notes. Just checking to see if it works." //Build Notes.
    symbolsDirectory = file("build/symbols/") // Directory for Symbols. Should be build/symbols
    mappingFileNameRegex = "R.txt" //file regex for mapping file. Should be R.txt
}






// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.0.+'
        classpath 'com.jimdo.gradle:gradle-apt-plugin:0.2-SNAPSHOT'
        classpath 'de.felixschulze.gradle:gradle-hockeyapp-plugin:1.3+'
    }
}

apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'

release {
    scm = 'git'
    preCommitText = 'RELEASE:'
    preTagCommitMessage = 'Release Build'
    tagCommitMessage = 'New Tag'
    newVersionCommitMessage = 'VERSION INCREMENTED'
}
/**
 * Returns the build version code.
 * This is either 9999 for a locally built apk, or the build server build number.
 * versionCode is fed in through command line arguments (-PversionCode) with the build server build count.
 * @return integer Version Code.
 */
def Integer buildVersionCode() {
    if (project.hasProperty('versionCode')) {
        return project.properties.versionCode.toInteger()
    } else {
        return 1;
    }
}

/**
 * Returns the build version name.
 * version needs to be in your gradle.properties. All else fails, it simply resorts to 1.0 as a version name
 * Gradle-Release-Plugin handles all version name incrementing
 * @return String Version Name.
 */
def String buildVersionName() {
    if (project.hasProperty('version')) {
        return project.properties.version
    } else {
        return "1.0";
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            credentials {
                username localMavenUser
                password localMavenPassword
            }
            url localMavenUrl
        }
    }
}

