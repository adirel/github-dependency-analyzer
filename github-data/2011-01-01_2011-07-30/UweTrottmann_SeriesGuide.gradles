apply plugin: 'android'
apply plugin: 'android-test'
apply plugin: 'crashlytics'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    productFlavors {
        free {
            packageName "com.battlelancer.seriesguide"
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName
        }
        internal {
            packageName "com.battlelancer.seriesguide.beta"
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName + "-internal"
        }
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile project(':libraries:androidutils')
    compile project(':libraries:pagerslidingtabstrip')
    compile project(':libraries:stickygridheaders')
    compile rootProject.ext.libraries.okhttp
    compile rootProject.ext.libraries.supportv4
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.github.chrisbanes.photoview:library:1.2.2'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.+'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'com.jakewharton.timber:timber:2.2.+'
    compile 'com.readystatesoftware.systembartint:systembartint:+'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile 'com.squareup.retrofit:retrofit:1.4.1'
    compile 'com.squareup:tape:1.1.1'
    compile 'de.greenrobot:eventbus:2.2.0'

    // OAuth 2.0
    compile 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:0.31'

    // App Engine
    compile ('com.google.api-client:google-api-client-android:1.17.0-rc') {
        exclude group: 'com.google.android.google-play-services'
    }
    compile 'com.google.http-client:google-http-client-gson:1.17.0-rc'
    compile 'com.google.android.gms:play-services:4.2.42'

    // libs folder
    compile fileTree(dir: 'libs', include: '*.jar')

    // unit tests
    testCompile 'junit:junit:4.10'
    testCompile 'com.squareup:fest-android:1.0.+'
    testCompile 'org.robolectric:robolectric:2.3-SNAPSHOT'

    // android instrumentation tests
    androidTestCompile ('com.jakewharton.espresso:espresso:1.1-r1') {
        // TODO workaround for http://b.android.com/65445
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestCompile ('com.jakewharton.espresso:espresso-support-v4:1.1-r1') {
        // TODO workaround for http://b.android.com/65445
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-v4'
    }

    // Crashlytics
    compile 'com.crashlytics.android:crashlytics:1.+'
}

// get signing config from gradle.properties file
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'http://download.crashlytics.com/maven'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.1-SNAPSHOT'
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'http://download.crashlytics.com/maven'
        }
    }
}

ext {
    compileSdkVersion = 19
    buildToolsVersion = '19.0.3'

    versionCode = 244
    versionName = '12.4'
}

ext.libraries = [

        supportv4: 'com.android.support:support-v4:19.0.+',
        okhttp: 'com.squareup.okhttp:okhttp:1.3.0'

]
apply plugin: 'android-library'

dependencies {
    compile rootProject.ext.libraries.supportv4
    compile rootProject.ext.libraries.okhttp
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}
apply plugin: 'android-library'

dependencies {
    compile rootProject.ext.libraries.supportv4
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}apply plugin: 'android-library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

    }
}
