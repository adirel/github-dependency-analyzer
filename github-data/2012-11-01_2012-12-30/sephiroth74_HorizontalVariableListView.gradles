buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.0+'
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}apply plugin: 'android'

dependencies {
    compile project(':library')
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        versionCode 1
        versionName '1.0.0'
        minSdkVersion 9
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}
apply plugin: 'android-library'
apply plugin: 'signing'

group 'it.sephiroth.android.library.horizontallistview'
version '1.2.1'

dependencies {
    compile 'com.android.support:support-v4:19.0.1+'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion 9
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

task displayInfo() {
    doLast {
        logger.lifecycle "  project name: ${project.name} (root: ${rootProject.name})"
        logger.lifecycle "  project version: ${project.version}"
        logger.lifecycle "  project group: ${project.group}"
    }
}


project.ext.outJar = project.name + '-' + version + '.jar'

task clearJar(type: Delete) {
    delete 'build/libs/' + project.ext.outJar
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', project.ext.outJar)
}

def generatePom() {
    def pom = pom {
        project {
            name 'HorizontalListView'
            groupId project.group
            artifactId project.name
            version project.version
            description 'Horizontal ListView for Android. Based on the official ListView google code.'
            inceptionYear '2013'
            url 'https://github.com/sephiroth74/HorizontalVariableListView'

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    distribution 'repo'
                }
            }

            scm {
                url "https://github.com/sephiroth74/HorizontalVariableListView"
                connection "scm:git:git@github.com:sephiroth74/HorizontalVariableListView.git"
                developerConnection "scm:git:git@github.com:sephiroth74/HorizontalVariableListView.git"
            }


            developers {
                developer {
                    id 'sephiroth74'
                    name 'Alessandro Crugnola'
                    email 'alessandro.crugnola@gmail.com'
                    url 'http://blog.sephiroth.it'
                    roles {
                        role 'author'
                        role 'developer'
                    }
                }

                developer {
                    id 'hborders'
                    name 'Heath Borders'
                    url 'http://heath-tech.blogspot.com/'
                    roles {
                        role 'contributor'
                    }
                }
            }
        }
    }
}

task writePom {
    ext.pomXml = file("pom.xml")
    inputs.file(project.buildFile)
    outputs.file(pomXml)
    doLast {
        generatePom().writeTo(pomXml)
    }
}


uploadArchives {
    if (hasProperty('sonatypeUsername')) {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                pom = generatePom()

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

signing {
    sign configurations.archives
}

artifacts {
    archives sourcesJar
}

clean.dependsOn(clearJar)
build.dependsOn(writePom, makeJar)
uploadArchives.dependsOn(build)
