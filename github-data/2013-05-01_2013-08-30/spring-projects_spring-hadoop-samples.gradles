buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RC1")
    }
}

version =  '0.1.0'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-milestone" }
}

dependencies {
    compile("org.springframework.data:spring-yarn-boot:2.0.0.M5")
}

task clientJar(type: Jar) {
    appendix = 'client'
    baseName = 'yarn-boot-simple'
    from sourceSets.main.output
    exclude('**/appmaster/*')
    exclude('**/container/*')
}

task appmasterJar(type: Jar) {
    appendix = 'appmaster'
    baseName = 'yarn-boot-simple'
    from sourceSets.main.output
    exclude('**/client/*')
    exclude('**/container/*')
}

task containerJar(type: Jar) {
    appendix = 'container'
    baseName = 'yarn-boot-simple'
    from sourceSets.main.output
    exclude('**/appmaster/*')
    exclude('**/client/*')
}

task clientBoot(type: BootRepackage, dependsOn: clientJar) {
    withJarTask = clientJar
}

task appmasterBoot(type: BootRepackage, dependsOn: appmasterJar) {
    withJarTask = appmasterJar
}

task containerBoot(type: BootRepackage, dependsOn: containerJar) {
    withJarTask = containerJar
}

jar.enabled = false
bootRepackage.enabled = false

task bootJars
bootJars.dependsOn = [clientBoot,containerBoot,appmasterBoot]

build.dependsOn(bootJars)

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
description = 'Spring Yarn Examples'
defaultTasks 'build'

apply plugin: 'base'

buildscript {
	repositories {
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath("org.springframework.build.gradle:docbook-reference-plugin:0.2.6")
	}
}

allprojects {
	group = 'org.springframework.yarn'
	//logging.captureStandardOutput LogLevel.DEBUG
 
	repositories {
		mavenCentral()
		maven { url 'http://repo.spring.io/libs-milestone' }
	}
}

def hadoopDefault = "hadoop22"
def hadoopDistro = project.hasProperty("distro") ? project.getProperty("distro") : hadoopDefault
def springDataVersion = hadoop22SpringDataVersion

switch (hadoopDistro) {

	// vanilla hadoop 2.2.x
	case "hadoop22":
		springDataVersion = hadoop22SpringDataVersion
		println "Using Apache Hadoop 2.2.x Spring Hadoop Dependencies - [$springDataVersion]"
	break;

	// pivotal

	default:
		if (!project.hasProperty("distro")) {
			println "Using default distro: Apache Hadoop Spring Hadoop Dependencies - [$springDataVersion]"
		} else {
			if (hadoopDistro == hadoopDefault) {
				println "Using Apache Hadoop 2.2.x Spring Hadoop Dependencies - [$springDataVersion]"
			} else {
				println "Failing build: $hadoopDistro is not a supported distro"
				println "Supported distros: hadoop22"
				println "* default"
				throw new InvalidUserDataException("$hadoopDistro is not a supported distro")
			}
		}
		springDataVersion = hadoop20SpringDataVersion

}

subprojects { subproject ->
	apply plugin: 'idea'
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
	
	eclipse {
		project {
			natures += 'org.springframework.ide.eclipse.core.springnature'
		}
	}

	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:-serial", "-Xlint:-options"]

	task sourcesJar(type: Jar) {
		classifier = 'sources'
	}

}

def yarnExampleProjects() {
	subprojects.findAll { project ->
		project.name.contains('yarn-examples-') && project.name != 'yarn-examples-common'
	}
}

def yarnExampleProjectsWithCommon() {
	subprojects.findAll { project ->
		project.name.contains('yarn-examples-')
	}
}

def yarnExampleProjectsCommon() {
	subprojects.findAll { project ->
		project.name == 'yarn-examples-common'
	}
}

configure(yarnExampleProjectsWithCommon()) {
	configurations {
		hadoopruntime.exclude group: 'log4j'
		hadoopruntime.exclude group: 'org.slf4j'
		hadoopruntime.exclude group: 'org.apache.hadoop'
		hadoopruntime.exclude group: 'org.apache.hive'
		hadoopruntime.exclude group: 'commons-logging'
		hadoopruntime.exclude group: 'org.codehaus.jettison'
		hadoopruntime.exclude group: 'com.thoughtworks.xstream'
	}
	dependencies {
		compile "org.springframework.data:spring-yarn-batch:$springDataVersion"
		runtime "org.springframework.data:spring-data-hadoop:$springDataVersion"
		runtime "org.springframework.data:spring-data-hadoop-core:$springDataVersion"
		runtime "log4j:log4j:$log4jVersion"
		runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
		testCompile "org.springframework.data:spring-yarn-test:$springDataVersion"
		testCompile "org.hamcrest:hamcrest-core:$hamcrestVersion"
		testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"

		hadoopruntime configurations.runtime
	}

	task testJar(type: Jar) {
		classifier = 'tests'
		from sourceSets.test.output
	}

	assemble.dependsOn = ['jar', 'testJar']

	clean.doLast {ant.delete(dir: "target")}

}

configure(yarnExampleProjectsCommon()) {
	task copyLibDeps(type: Copy) {
		into "$buildDir/dependency-libs"
		from configurations.hadoopruntime
	}
}

configure(yarnExampleProjects()) { yarnExampleProject ->
	dependencies {
		compile project(":yarn-examples-common")
	}
	
	task run(type: JavaExec) {
		description = 'Runs the application'
		classpath = sourceSets.main.runtimeClasspath
		standardInput = System.in
		systemProperties = System.getProperties()
		if (rootProject.hasProperty('args')) {
			args = ["${rootProject.getProperty('args')}"]
		}  
	}

	task copyHadoopRuntimeDeps(type: Copy) {
		into "$buildDir/dependency-libs"
		from configurations.hadoopruntime
	}

	test.dependsOn(copyHadoopRuntimeDeps)
	build.dependsOn(copyHadoopRuntimeDeps)

	apply {
		task "run-${yarnExampleProject.name}"(dependsOn:'classes') << {
			def mainClass = yarnExampleProject.hasProperty('main')? "${yarnExampleProject.getProperty('main')}":'CommonMain'
			def nokillArg = yarnExampleProject.hasProperty('nokill') ? "${yarnExampleProject.getProperty('nokill')}" : null
			def appidArg = yarnExampleProject.hasProperty('appid') ? "${yarnExampleProject.getProperty('appid')}" : null
			def args = []
			yarnExampleProject.tasks['run'].main = "org.springframework.yarn.examples.$mainClass"
			if (nokillArg) {
				args.add("nokill=$nokillArg")
			}
			if (appidArg) {
				args.add("appid=$appidArg")
			}
			yarnExampleProject.tasks['run'].args = args
			yarnExampleProject.tasks['run'].execute()
		}
	}
}

description = 'Spring Yarn Examples Common'

project('yarn-examples-simple-command') {
  description = 'Spring Yarn with Simple Command Example'
}

project('yarn-examples-list-applications') {
  description = 'Spring Yarn with List Applications Example'
  ext.main = 'Main'
}

project('yarn-examples-multi-context') {
  description = 'Spring Yarn with Multi Context Example'
}

project('yarn-examples-kill-application') {
  description = 'Spring Yarn with Kill Application Example'
  ext.main = 'Main'
}

project('yarn-examples-batch-partition') {
  description = 'Spring Yarn with Batch Partition Example'
}

project('yarn-examples-batch-files') {
  description = 'Spring Yarn with Batch Files Example'
}

project('yarn-examples-batch-amjob') {
  description = 'Spring Yarn Batch Application Master Job Example'
}

project('yarn-examples-custom-amservice') {
  description = 'Spring Yarn with Custom Application Master Service Example'
}

project('yarn-examples-restart-context') {
  description = 'Spring Yarn with Restart Context Example'
}
