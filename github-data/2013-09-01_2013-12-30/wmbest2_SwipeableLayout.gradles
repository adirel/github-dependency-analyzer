// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

task wrapper(type: Wrapper) { gradleVersion = '1.8' }
apply plugin: 'android-library'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.nineoldandroids:library:2.4.+'
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 18
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.4"
group = "com.wmbest.widget"

def deployRepo = sonatypeRepo

/* Uncomment to deploy locally */
//deployRepo = 'file://localhost/tmp/myRepo'

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: deployRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            artifactId 'swipeable-layout'
            name 'Android Swipeable Layout Library'
            packaging 'aar'
            description 'A auto-resizing swipeable layout'
            url 'https://github.com/wmbest2/SwipeableLayout'

            scm {
                url 'scm:git@github.com:wmbest2/SwipeableLayout.git'
                connection 'scm:git@github.com:wmbest2/SwipeableLayout.git'
                developerConnection 'scm:git@github.com:wmbest2/SwipeableLayout.git'
            }

            licenses {
                license {
                    name 'GNU General Public License (GPL), Version 2.0'
                    url 'http://www.gnu.org/licenses/gpl2.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'wmbest2'
                    name 'William Best'
                    email 'wmbest2@gmail.com'
                }
            }
        }
    }
}

