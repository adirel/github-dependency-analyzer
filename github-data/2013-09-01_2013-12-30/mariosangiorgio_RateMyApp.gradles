['android-library', 'signing', 'maven'].each {
    apply plugin: it
}

group = 'com.mariosangiorgio'
version = '1.2'

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 16
    }

    buildTypes {
        release {
            runProguard false
            proguardFile 'proguard-rules.txt'
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:appcompat-v7:+'
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'RateMyApp Android Library'
            packaging 'aar'
            description 'An Android module to kindly ask users to rate your application.'
            url 'https://github.com/mariosangiorgio/RateMyApp'

            scm {
                url 'scm:git@github.com:mariosangiorgio/RateMyApp'
                connection 'scm:git@github.com:mariosangiorgio/RateMyApp.git'
                developerConnection 'scm:git@github.com:mariosangiorgio/RateMyApp.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://github.com/mariosangiorgio/RateMyApp/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'mariosangiorgio'
                    name 'Mario Sangiorgio'
                    email 'mariosangiorgio@gmail.com'
                }
            }
        }
    }
}
