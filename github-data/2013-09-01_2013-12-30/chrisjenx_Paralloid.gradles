buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'uk.co.chrisjenx.paralloid'
    version = '0.3.0'

    ext.isReleaseVersion = has("release")
    if (isReleaseVersion) {
        ext.sonatypeRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    } else {
        version += "-SNAPSHOT"
        ext.sonatypeRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    ext.sonatypeUsername = has("sonatypeUsername") ? sonatypeUsername : ""
    ext.sonatypePassword = has("sonatypePassword") ? sonatypePassword : ""

    apply plugin: 'maven'
    apply plugin: 'signing'

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    signing {
        required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android-library'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }
}

dependencies {
    compile "com.android.support:support-v4:18.0.+"
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        
        repository(url: sonatypeRepo) {
            authentication(
                    userName: sonatypeUsername,
                    password: sonatypePassword
            )
        }

        pom.project {
            name 'Paralloid'
            artifactId = 'paralloid'
            packaging 'aar'
            description 'Parallax library for Android'
            url 'https://github.com/chrisjenx/Paralloid'

            scm {
                url 'scm:git@github.com:chrisjenx/Paralloid.git'
                connection 'scm:git@github.com:chrisjenx/Paralloid.git'
                developerConnection 'scm:git@github.com:chrisjenx/Paralloid.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'chrisjenx'
                    name 'Christopher Jenkins'
                    email 'chris.mark.jenkins@gmail.com'
                }
            }
        }
    }
}
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 18
    }

    signingConfigs {
        release {
            // We can leave these in environment variables
            storeFile file("keystore/paralloidkeystore.jks")
            keyAlias "paralloid"

            storePassword 'paralloid'
            keyPassword 'paralloid'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
	compile 'com.android.support:appcompat-v7:18.0.+'
    compile project(':paralloidviews')
}
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android-library'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }
}

dependencies {
    compile project(':paralloid')
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(
                    userName: sonatypeUsername,
                    password: sonatypePassword
            )
        }

        pom.project {
            name 'Paralloid Views'
            artifactId = 'paralloidviews'
            packaging 'aar'
            description 'Parallax library for Android Extra Views'
            url 'https://github.com/chrisjenx/Paralloid'

            scm {
                url 'scm:git@github.com:chrisjenx/Paralloid.git'
                connection 'scm:git@github.com:chrisjenx/Paralloid.git'
                developerConnection 'scm:git@github.com:chrisjenx/Paralloid.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'chrisjenx'
                    name 'Christopher Jenkins'
                    email 'chris.mark.jenkins@gmail.com'
                }
            }
        }
    }
}
